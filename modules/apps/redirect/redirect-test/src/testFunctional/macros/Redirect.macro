definition {

	macro addEntry {
		var portalURL = PropsUtil.get("portal.url");

		LexiconEntry.gotoAdd();

		AssertTextEquals(
			locator1 = "TextInput#REDIRECT_SOURCE_URL_PREFIX",
			value1 = "${portalURL}/web/${siteName}");

		Type(
			locator1 = "TextInput#REDIRECT_SOURCE_URL",
			value1 = "${sourceURL}");

		Type(
			locator1 = "TextInput#REDIRECT_DESTINATION_URL",
			value1 = "${destinationURL}");

		if (isSet(redirectType)) {
			Select(
				locator1 = "Select#REDIRECT_TYPE",
				value1 = "${redirectType}");
		}

		if (isSet(expirationDate)) {
			Type.clickAtType(
				locator1 = "TextInput#EXPIRATION_DATE",
				value1 = "${expirationDate}");
		}

		Button.clickSave();

		if ("${redirectURLVariation}" == "Null") {
			Alert.viewRequiredField();
		}

		else if ("${sourceURLVariation}" == "Duplicate") {
			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR",
				value1 = "Error:There is already a redirect set for the same source URL.");
		}

		else {
			Alert.viewSuccessMessage();
		}
	}

	macro deleteEntry {
		if (isSet(deletions)) {
			ClickNoError(locator1 = "Icon#DELETE");
		}

		else {
			Redirect.gotoEntryMenuItem(
				item = "Delete",
				sourceURL = "${sourceURL}");
		}

		Alert.viewSuccessMessage();
	}

	macro editEntry {
		Redirect.gotoEntryMenuItem(
			item = "Edit",
			sourceURL = "${sourceURL}");

		if (isSet(sourceURLEdit)) {
			Type(
				locator1 = "TextInput#REDIRECT_SOURCE_URL",
				value1 = "${sourceURLEdit}");
		}

		if (isSet(destinationURLEdit)) {
			Type(
				locator1 = "TextInput#REDIRECT_DESTINATION_URL",
				value1 = "${destinationURLEdit}");
		}

		if (isSet(redirectTypeEdit)) {
			Select(
				locator1 = "Select#REDIRECT_TYPE",
				value1 = "${redirectTypeEdit}");
		}

		if (isSet(expirationDate)) {
			Type.clickAtType(
				locator1 = "TextInput#EXPIRATION_DATE",
				value1 = "${expirationDate}");
		}

		Button.clickSave();
	}

	macro gotoEntryMenuItem {
		LexiconTable.clickEllipsisItem(
			item = "${item}",
			tableEntry = "${sourceURL}");
	}

	macro ignoreEntry {
		if ("${unignore}" == "true") {
			AssertClick(
				key_navItem = "404 URLs",
				locator1 = "NavBar#NAV_ITEM_REDIRECTION",
				value1 = "404 URLs");

			Click(locator1 = "Dropdown#FILTER_AND_ORDER");

			MenuItem.click(menuItem = "Ignored URLs");

			Redirect.gotoEntryMenuItem(
				item = "Unignore",
				sourceURL = "${ignoreURL}");

			Alert.viewSuccessMessage();
		}
		else {
			AssertClick(
				key_navItem = "404 URLs",
				locator1 = "NavBar#NAV_ITEM_REDIRECTION",
				value1 = "404 URLs");

			Click(locator1 = "Dropdown#FILTER_AND_ORDER");

			MenuItem.click(menuItem = "Active URLs");

			Redirect.gotoEntryMenuItem(
				item = "Ignore",
				sourceURL = "${ignoreURL}");

			Alert.viewSuccessMessage();
		}
	}

	macro openRedirectAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_redirect_web_internal_portlet_RedirectPortlet");
	}

	macro view404URLs {
		if (isSet(activeIgnoreURL)) {
			AssertClick(
				key_navItem = "404 URLs",
				locator1 = "NavBar#NAV_ITEM_REDIRECTION",
				value1 = "404 URLs");
		}

		AssertTextEquals.assertPartialText(
			key_rowEntry = "${ignoreURL}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME",
			value1 = "${ignoreURL}");

		AssertTextEquals.assertPartialText(
			key_rowEntry = "${requestNumber}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME",
			value1 = "${requestNumber}");
	}

	macro view404URLsDefault {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO_TAGLIB_TITLE",
			value1 = "All is in order.");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO_TAGLIB_DESCRIPTION",
			value1 = "All your pages are connected or redirected.");
	}

	macro viewDefault {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO_TAGLIB",
			value1 = "No redirects were found.");
	}

	macro viewEntry {
		AssertTextEquals.assertPartialText(
			key_rowEntry = "${sourceURL}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${sourceURL}");

		AssertTextEquals.assertPartialText(
			key_rowEntry = "${destinationURL}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${destinationURL}");

		if (isSet(redirectType)) {
			AssertTextEquals(
				key_rowEntry = "${redirectType}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME",
				value1 = "${redirectType}");
		}

		if (isSet(expirationDate)) {
			AssertTextEquals(
				key_rowEntry = "${expirationDate}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME",
				value1 = "${expirationDate}");
		}
	}

	macro viewOrderedURLs {
		AssertTextEquals(
			key_rowNumber = "1",
			locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER_TD_2",
			value1 = "${requestNumber1}");

		AssertTextEquals(
			key_rowNumber = "2",
			locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER_TD_2",
			value1 = "${requestNumber2}");
	}

}
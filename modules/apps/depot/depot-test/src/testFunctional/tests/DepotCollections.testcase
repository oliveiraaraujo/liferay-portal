@component-name = "portal-document-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Depot";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();
		}
	}

	@description = "This ensures that the documents with document type can be selected through Collections in a depot."
	@priority = "5"
	test AddDocTypeDocumentsManually {
		for (var i : list "1,2") {
			DepotNavigator.openDepotAdmin();

			DepotNavigator.gotoDepot(
				depotName = "Test Depot Name",
				portlet = "Documents and Media");

			DMNavigator.gotoDocumentType();

			DMDocumentType.add(
				dmDocumentTypeDescription = "DM Document Type Description",
				dmDocumentTypeFieldNames = "Text Box",
				dmDocumentTypeName = "DM Document Type Name ${i}");
		}

		for (var i : list "1,2") {
			DepotNavigator.openDepotAdmin();

			DepotNavigator.gotoDepot(
				depotName = "Test Depot Name",
				portlet = "Documents and Media");

			DMDocument.addCP(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "DM Document Title ${i}",
				dmDocumentTypeName = "DM Document Type Name 2");
		}

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addManualSelection(
			assetListTitle = "Depot Manual Asset List",
			depotName = "Test Depot Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Manual Asset List",
			assetType = "Web Content Folder,Basic Document,Basic Web Content,Documents Folder,DM Document Type Name 1,DM Document Type Name 2",
			manualSelection = "true");

		for (var i : list "1,2") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.selectAsset(
				assetListName = "Depot Manual Asset List",
				assetName = "DM Document Title ${i}",
				assetType = "DM Document Type Name 2");
		}

		for (var documentName : list "DM Document Title 1,DM Document Title 2") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Manual Asset List",
				assetTitle = "${documentName}",
				assetType = "Document");
		}
	}

	@description = "This ensures that the scope can be changed through dynamic selection in a depot."
	@priority = "5"
	test ChangeScope {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC Depot Title");

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Site Name",
			title = "WC Site Title");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addDynamicSelection(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			otherSite = "Test Site Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.removeScope(
			assetListTitle = "Depot Dynamic Asset List",
			scopeName = "Current Asset Library");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "WC Site Title",
			assetType = "Web Content Article");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewNoContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "WC Depot Title",
			assetType = "Web Content Article");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			defaultSite = "Current Asset Library (Test Depot Name)");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.removeScope(
			assetListTitle = "Depot Dynamic Asset List",
			scopeName = "Test Site Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "WC Depot Title",
			assetType = "Web Content Article");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewNoContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "WC Site Title",
			assetType = "Web Content Article");
	}

	@description = "This ensures that collections from a site can select the basic web content stored in a depot."
	@priority = "5"
	test CreateSetOnSiteUsingDepotWC {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Asset Publisher Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Asset Publisher Page",
			widgetName = "Asset Publisher");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "site-name");

		AssetListsAdmin.addManualSelection(assetListTitle = "Site Manual Asset List");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "site-name");

		AssetListsAdmin.selectAsset(
			assetListName = "Site Manual Asset List",
			assetName = "WC WebContent Title",
			assetType = "Basic Web Content",
			depotName = "Test Depot Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssetPublisherPortlet.selectAssetList(assetListName = "Site Manual Asset List");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssertTextEquals(
			key_assetTitle = "WC WebContent Title",
			locator1 = "AP#ASSET_ABSTRACT_TITLE_EXACT_MATCH",
			value1 = "WC WebContent Title");
	}

	@description = "This ensures that the asset types will not be displayed when disabling the applications."
	@priority = "5"
	test DisableAllAssetTypes {
		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addDynamicSelection(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addManualSelection(assetListTitle = "Depot Manual Asset List");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			category = "Configuration",
			depotName = "Test Depot Name",
			portlet = "Settings");

		Depot.configureApplication(
			applicationList = "Documents and Media,Web Content",
			status = "Disabled");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetTypeList(
			allAssetsDisabled = "ture",
			assetListName = "Depot Manual Asset List",
			manualSelection = "true");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetTypeList(
			allAssetsDisabled = "true",
			assetListName = "Depot Dynamic Asset List",
			dynamicSelection = "true");
	}

	@description = "This ensures that only the asset types related to web content will be displayed after disabling DM."
	@priority = "5"
	test DisableDM {
		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addDynamicSelection(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addManualSelection(assetListTitle = "Depot Manual Asset List");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-depot-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DynamicDataMapping.addField(
			field = "Text",
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			category = "Configuration",
			depotName = "Test Depot Name",
			portlet = "Settings");

		Depot.configureApplication(
			applicationList = "Documents and Media",
			status = "Disabled");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Manual Asset List",
			assetType = "Web Content Folder,Basic Web Content,WC Structure Name",
			manualSelection = "true");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Dynamic Asset List",
			assetType = "Web Content Folder,Web Content Article",
			dynamicSelection = "true");
	}

	@description = "This ensures that only the asset types related to DM will be displayed after disabling WC."
	@priority = "5"
	test DisableWC {
		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addDynamicSelection(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addManualSelection(assetListTitle = "Depot Manual Asset List");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-depot-name");

		DMNavigator.gotoDocumentType();

		DMDocumentType.add(
			dmDocumentTypeDescription = "DM Document Type Description",
			dmDocumentTypeFieldNames = "Text Box",
			dmDocumentTypeName = "DM Document Type Name");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			category = "Configuration",
			depotName = "Test Depot Name",
			portlet = "Settings");

		Depot.configureApplication(
			applicationList = "Web Content",
			status = "Disabled");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Manual Asset List",
			assetType = "Basic Document,Documents Folder,DM Document Type Name",
			manualSelection = "true");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Dynamic Asset List",
			assetType = "Document,Documents Folder",
			dynamicSelection = "true");
	}

	@description = "This ensures that the image is displayed by AP through Collections in a depot."
	@priority = "5"
	test DisplayImageInAP {
		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-depot-name");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_2.jpeg",
			dmDocumentTitle = "Document Title");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addManualSelection(
			assetListTitle = "Depot Manual Asset List",
			depotName = "Test Depot Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetList(
			assetListTitle = "Depot Manual Asset List",
			type = "Manual");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.selectAsset(
			assetListName = "Depot Manual Asset List",
			assetName = "Document Title",
			assetType = "Basic Document");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Asset Publisher Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Asset Publisher Page",
			widgetName = "Asset Publisher");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssetPublisherPortlet.selectAssetList(
			assetListName = "Depot Manual Asset List",
			depotName = "Test Depot Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssertTextEquals(
			key_assetTitle = "Document Title",
			locator1 = "AP#ASSET_ABSTRACT_TITLE_EXACT_MATCH",
			value1 = "Document Title");
	}

	@description = "This ensures that tagged the web content is displayed by AP through dynamic selection in a depot."
	@priority = "5"
	test DisplayWebContentWithTagInAP {
		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-depot-name");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_2.doc",
			dmDocumentTitle = "Document Title");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-depot-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithTagCP(
			tagNameList = "tag1",
			webContentContent = "Web Content Content",
			webContentTitle = "Web Content Title");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addDynamicSelection(
			assetListTitle = "Depot Dynamic Asset List",
			depotName = "Test Depot Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetList(
			assetListTitle = "Depot Dynamic Asset List",
			type = "Dynamic");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.editAssetList(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.configureAssetEntryType(selectedAsset = "Web Content Article");

		AssetListsAdmin.addTagsFilter(tagNameList = "tag1");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Asset Publisher Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Asset Publisher Page",
			widgetName = "Asset Publisher");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssetPublisherPortlet.selectAssetList(
			assetListName = "Depot Dynamic Asset List",
			depotName = "Test Depot Name");

		Navigator.gotoSitePage(
			pageName = "Asset Publisher Page",
			siteName = "Site Name");

		AssertTextEquals(
			key_assetTitle = "Web Content Title",
			locator1 = "AP#ASSET_ABSTRACT_TITLE_EXACT_MATCH",
			value1 = "Web Content Title");
	}

	@description = "This ensures that images are excluded with the dynamic selection that does not contain the keyword "Image" in a depot."
	@priority = "5"
	test ExcludeImagesDynamically {
		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-depot-name");

		for (var i : list "1,2") {
			DMDocument.addCP(
				dmDocumentDescription = "DM Document Description",
				dmDocumentFile = "Document_${i}.doc",
				dmDocumentTitle = "Document Text Title ${i}");
		}

		for (var i : list "1,2") {
			DMDocument.addCP(
				dmDocumentDescription = "DM Document Description",
				dmDocumentFile = "Document_${i}.png",
				dmDocumentTitle = "Document Image Title ${i}");
		}

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addDynamicSelection(
			assetListTitle = "Depot Dynamic Asset List",
			depotName = "Test Depot Name");

		AssetListsAdmin.addKeywordsFilter(
			keyWords = "Image",
			queryAndOperator = "Any",
			queryContains = "Does Not Contain");

		for (var i : list "1,2") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "Document Text Title ${i}",
				assetType = "Document");
		}
	}

	@description = "This ensures that the asset can be removed in Collections."
	@priority = "4"
	test RemoveAssetFromContentSet {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-depot-name");

		for (var i : list "1,2,3") {
			WebContentNavigator.gotoAddCP();

			WebContent.addCP(
				webContentContent = "WC WebContent Content",
				webContentTitle = "WC WebContent Title ${i}");

			PortletEntry.publish();
		}

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addManualSelection(
			assetListTitle = "Depot Manual Asset List",
			depotName = "Test Depot Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetList(
			assetListTitle = "Depot Manual Asset List",
			type = "Manual");

		for (var i : list "1,2,3") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.selectAsset(
				assetListName = "Depot Manual Asset List",
				assetName = "WC WebContent Title ${i}",
				assetType = "Basic Web Content");
		}

		AssetListsAdmin.deleteManualSelectionAssets(assetsList = "WC WebContent Title 2");

		for (var i : list "1,3") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Manual Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewNoContent(
			assetListTitle = "Depot Manual Asset List",
			assetTitle = "WC WebContent Title 2",
			assetType = "Web Content Article");
	}

	@description = "This ensures that only web contents configured to show with dynamic selection are displayed in a depot."
	@priority = "4"
	test SelectAllWebContentDynamically {
		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-depot-name");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "Document Title");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-depot-name");

		for (var i : list "1,2,3") {
			WebContentNavigator.gotoAddCP();

			WebContent.addCP(
				webContentContent = "WC WebContent Content",
				webContentTitle = "WC WebContent Title ${i}");

			PortletEntry.publish();
		}

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addDynamicSelection(
			assetListTitle = "Depot Dynamic Asset List",
			depotName = "Test Depot Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetTypeList(
			assetListName = "Depot Dynamic Asset List",
			assetType = "Web Content Folder,Web Content Article,Document,Documents Folder",
			dynamicSelection = "true");

		for (var i : list "1,2,3") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "Document Title",
			assetType = "Document");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.editAssetList(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.configureAssetEntryType(selectedAsset = "Web Content Article");

		for (var i : list "1,2,3") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewNoContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "Document Title",
			assetType = "Document");
	}

	@description = "This ensures that an image can be selected from other site scope through dynamic selection that does not contain the keyword."
	@priority = "5"
	test SelectDocumentFromOtherScopeDynamically {
		JSONGroup.addGroup(groupName = "Test Site Name");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-site-name");

		DMDocument.addMultipleCP(dmDocumentFileList = "Document_1.doc,Document_2.doc");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-site-name");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_2.jpeg",
			dmDocumentTitle = "DM Image Title");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addDynamicSelection(
			assetListTitle = "Depot Dynamic Asset List",
			depotName = "Test Depot Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			otherSite = "Test Site Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.editAssetList(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.addKeywordsFilter(
			keyWords = "doc",
			queryAndOperator = "Any",
			queryContains = "Does Not Contain");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "DM Image Title",
			assetType = "Document");

		for (var i : list "1,2") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.viewNoContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "Document_${i}.doc",
				assetType = "Document");
		}
	}

	@description = "This ensures that multiple documents can be selected through Collections in a depot."
	@priority = "5"
	test SelectMultipleDocumentsManually {
		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-depot-name");

		DMDocument.addMultipleCP(dmDocumentFileList = "Document_1.doc,Document_2.jpg,Document_3.txt");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addManualSelection(assetListTitle = "Depot Manual Asset List");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetList(
			assetListTitle = "Depot Manual Asset List",
			type = "Manual");

		for (var documentName : list "Document_1.doc,Document_2.jpg,Document_3.txt") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.selectAsset(
				assetListName = "Depot Manual Asset List",
				assetName = "${documentName}",
				assetType = "Basic Document");
		}

		for (var documentName : list "Document_1.doc,Document_2.jpg,Document_3.txt") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Manual Asset List",
				assetTitle = "${documentName}",
				assetType = "Document");
		}
	}

	@description = "This ensures that multiple web content can be selected through Collections in a depot."
	@priority = "5"
	test SelectMultipleWebContentManually {
		for (var i : list "1,2,3") {
			JSONWebcontent.addWebContent(
				content = "WC WebContent Content",
				groupName = "Test Depot Name",
				site = "false",
				title = "WC WebContent Title ${i}");
		}

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addManualSelection(
			assetListTitle = "Depot Manual Asset List",
			depotName = "Test Depot Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewAssetList(
			assetListTitle = "Depot Manual Asset List",
			type = "Manual");

		for (var i : list "1,2,3") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.selectAsset(
				assetListName = "Depot Manual Asset List",
				assetName = "WC WebContent Title ${i}",
				assetType = "Basic Web Content");
		}

		for (var i : list "1,2,3") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Manual Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}
	}

	@description = "This ensures that web contents from all the scopes can be selected through dynamic selection in a depot."
	@priority = "4"
	test SelectWCFromAllScopesDynamically {
		JSONGroup.addGroup(groupName = "Test Site Name 1");

		JSONGroup.addGroup(groupName = "Test Site Name 2");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title 1");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Site Name 1",
			title = "WC WebContent Title 2");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Site Name 2",
			title = "WC WebContent Title 3");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addDynamicSelection(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			otherSite = "Test Site Name 1");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			otherSite = "Test Site Name 2");

		for (var i : list "1,2,3") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}
	}

	@description = "This ensures that a web content can be selected from other site scope through dynamic selection that does not contain the keyword."
	@priority = "5"
	test SelectWCFromOtherScopeDynamically {
		JSONGroup.addGroup(groupName = "Test Site Name");

		for (var i : list "1 2,2 3,3 4") {
			JSONWebcontent.addWebContent(
				content = "WC WebContent Content",
				groupName = "Test Site Name",
				title = "WC WebContent Title ${i}");
		}

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addDynamicSelection(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.configureScope(
			assetListTitle = "Depot Dynamic Asset List",
			otherSite = "Test Site Name");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.editAssetList(assetListTitle = "Depot Dynamic Asset List");

		AssetListsAdmin.addKeywordsFilter(
			keyWords = "3",
			queryAndOperator = "Any",
			queryContains = "Does Not Contain");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.viewContent(
			assetListTitle = "Depot Dynamic Asset List",
			assetTitle = "WC WebContent Title 1 2",
			assetType = "Web Content Article");

		for (var i : list "2 3,3 4") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.viewNoContent(
				assetListTitle = "Depot Dynamic Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}
	}

	@description = "This ensures that the web content created by the structure can be selected via searching for the corresponding structure name."
	@priority = "5"
	test SelectWebContentWithStructure {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-depot-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DynamicDataMapping.addField(
			field = "Text",
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-depot-name");

		for (var i : list "1,2") {
			WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

			WebContent.addWithStructureCP(
				webContentText = "WC WebContent Text ${i}",
				webContentTitle = "WC WebContent Title ${i}");

			PortletEntry.publish();
		}

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

		AssetListsAdmin.addManualSelection(assetListTitle = "Depot Manual Asset List");

		for (var i : list "1,2") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.selectAsset(
				assetListName = "Depot Manual Asset List",
				assetName = "WC WebContent Title ${i}",
				assetType = "WC Structure Name");
		}

		for (var i : list "1,2") {
			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-depot-name");

			AssetListsAdmin.viewContent(
				assetListTitle = "Depot Manual Asset List",
				assetTitle = "WC WebContent Title ${i}",
				assetType = "Web Content Article");
		}
	}

	@description = "This ensures that Collections can be set in a depot."
	@priority = "5"
	test Smoke {
		property portal.acceptance = "true";
		property testray.component.names = "Asset Lists";

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepot(
			depotName = "Test Depot Name",
			portlet = "Collections");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO_TAGLIB_TITLE",
			value1 = "No Collections yet.");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO_TAGLIB_DESCRIPTION",
			value1 = "Fortunately, it is very easy to add new ones.");

		LexiconEntry.gotoAdd();

		MenuItem.viewPresent(menuItem = "Manual Selection");

		MenuItem.viewPresent(menuItem = "Dynamic Selection");

		Refresh();

		Click(locator1 = "Message#EMPTY_INFO_TAGLIB");

		Click.waitForMenuToggleJSClick(locator1 = "Dropdown#EMPTY_MESSAGE_DROPDOWN");

		AssertElementPresent(
			key_menuItem = "Manual Selection",
			locator1 = "MenuItem#DROPDOWN_MENU_ITEM");

		AssertElementPresent(
			key_menuItem = "Dynamic Selection",
			locator1 = "MenuItem#DROPDOWN_MENU_ITEM");
	}

}